package org.ollabaca.on.mm.org

import org.eclipse.uml2.uml.NamedElement
import org.eclipse.uml2.uml.PackageableElement

abstract class OrgElement extends PackageableElement {
	
}

abstract class Agent extends NamedElement, OrgElement {
	
	/**
	 * cf. http://www.w3.org/TR/2012/WD-vocab-org-20121023/#org:hasMembership
	 */
	refers readonly Membership[] memberships opposite members
}

class Person extends Agent {
	
	
}

class Role extends NamedElement, OrgElement {
	
}

/**
 * Relationships between Role & Membership is detailed here : http://www.epimorphics.com/web/wiki/organization-ontology-second-draft
 * 
 */
class Membership {
	
	/**
	 * The organization that contains this membership
	 */
	refers readonly Organization[1] organization opposite members
	
	/**
	 * Role played by members
	 */
	refers Role[1] role
	
	refers Agent[] members opposite memberships
}

class Organization extends Agent {
	
	String purpose
	
	/**
	 * List of memberships
	 * 
	 * Cf. http://www.w3.org/TR/2012/WD-vocab-org-20121023/#org:member
	 */
	contains Membership[] members opposite organization
	
	/**
	 * Sub organizations
	 */
	contains Organization[] organizations opposite parent
	
	refers Organization parent opposite organizations
	
}


